letter = { (LETTER | NUMBER | PUNCTUATION | SYMBOL) }
word = { letter+ }
space = { SEPARATOR+ }
phrase = { (word ~ SEPARATOR+)+ }

vowel_tilde = { ("á"|"é"|"í"|"ó"|"ú"|"Á"|"É"|"Í"|"Ó"|"Ú") }
vowel_ntilde = { ("a"|"e"|"i"|"o"|"u"|"A"|"E"|"I"|"O"|"U") }
vowel = { (vowel_ntilde | vowel_tilde) }
closed_vowel = { ("e"|"i"|"é"|"í"|"E"|"I"|"É"|"Í") }
open_vowel = { ("a"|"o"|"u"|"á"|"ó"|"ú"|"A"|"O"|"U"|"Á"|"Ó"|"Ú") }
hat_vowel = { ("â"|"ê"|"î"|"ô"|"û"|"Â"|"Ê"|"Î"|"Ô"|"Û") }
hat_closed_vowel = { ("ê"|"î"|"Ê"|"Î") }

// supress muted /h/

H = { ("h" | "H") }
nH = { 'a'..'g' | 'i'..'z' | 'A'..'G' | 'I'..'Z' }
initial_h = { H ~ letter }
inner_ch = { ("c" | "C") ~ H ~ letter }
inner_h = { H ~ letter }
hua = { H ~ ("ua" | "UA" | "Ua" | "uA") }
hue = { H ~ ("ue" | "UE" | "Ue" | "uE") }
noh = { nH+ }
h = _{ (initial_h* ~ ((hua | hue | inner_ch | inner_h) | noh+)+ ~ space?)+ }

// Replacement rules for /ks/ with EPA VAF

X = { ("x" | "X") }
// If the text begins with /ks/
// Xilófono roto => Çilófono roto
initial_x = { X ~ letter }
// If the /ks/ sound is between vowels
// Axila => Aççila | Éxito => Éççito
inner_vowel_x = { vowel ~ X ~ vowel }

x = _{ (initial_x* ~ ((inner_vowel_x) | letter+)+ ~ space?)+ }

// Replacement rules for /∫/ (voiceless postalveolar fricative)

C = { ("c" | "C") }
CH = { C ~ H }
ch = _{ ((CH | letter)+ ~ space?)+ }

// Replacing /x/ (voiceless postalveolar fricative) with /h/

G = { ("g" | "G") }
J = { ("j" | "J") }
U = { ("u" | "U") }
Ud = { ("ü" | "Ü") }
B = { ("b" | "B") }
M = { ("m" | "M") }
E = { ("e" | "E") }
N = { ("n" | "N") }
L = { ("l" | "L") }
S = { ("s" | "S") }
nM = { 'a'..'l' | 'n'..'z' | 'A'..'L' | 'N'..'Z' }
GJ = { ( G | J ) }
GJ1 = { GJ ~ closed_vowel }
GJ2 = { J ~ open_vowel }
GJV = { GJ1 | GJ2 }
GU = { (G ~ U) }
GUd = { (G ~ Ud) }
GUE = { (GU ~ closed_vowel) }
GUEd = { (GUd ~ closed_vowel) }
BUE1 = { B ~ U ~ E ~ (N|L|S) }
BUE = { nM ~ B ~ U ~ E ~ (N|L|S) }
gj = _{ ((GJV | GUE | GUEd | BUE | BUE1 | letter)+ ~ space?)+ }

// Replacing all /v/ (Voiced labiodental fricative) with /b/

V = { ("v" | "V") }
NV = { N ~ V }
v = _{ ((NV | V | letter)+ ~ space?)+ }

// Replacing /ʎ/ (digraph ll) with Greek Y for /ʤ/ sound (voiced postalveolar affricate)

LL = { L ~ L }
ll = _{ ((LL | letter)+ ~ space?)+ }

// Rotating /l/ with /r/

Cc = { ("ç" | "Ç") }
D = { ("d" | "D" ) }
F = { ("f" | "F" ) }
K = { ("k" | "K" ) }
P = { ("p" | "P" ) }
Q = { ("q" | "Q" ) }
R = { ("r" | "R" ) }
T = { ("t" | "T" ) }
Z = { ("z" | "Z" ) }
Lnext = { (B|C|Cc|G|S|D|F|G|H|K|M|P|Q|R|T|X|Z) }
l = _{ (((L ~ Lnext) | letter)+ ~ space?)+ }

// Drops /p/ for pseudo- or psico- prefixes

I = { ("i" | "I" ) }
PSIC = { P ~ S ~ I ~ C }
PSEUD = { P ~ S ~ E ~ U ~ D }
psico = _{ ((PSIC|PSEUD|letter)+ ~ space?)+ }

// Replacing Voiceless alveolar fricative (vaf) /s/ /θ/ with EPA's ç/Ç

ZSv = { (Z|S) ~ (vowel|hat_vowel) }
Cv = { C ~ (closed_vowel|hat_closed_vowel) }
vaf = _{ ((ZSv | Cv | letter)+ ~ space?)+ }

// Word ending rules
ENDING_D = { (vowel ~ D) }
ENDING_S = { (vowel ~ S) }
ENDING_CONS = { (vowel ~ (B|C|F|G|J|K|L|P|R|T|X|Z)) }
ENDING_PS = { (E ~ P ~ S) }
INTER_D = { ("a"|"A"|"i"|"I"|"í"|"Í") ~ D ~ ("o"|"O"|"a"|"A") ~ S? }

ENDING_RULE = { (INTER_D | ENDING_PS | ENDING_D | ENDING_S | ENDING_CONS) ~ (space | EOI) }
ENDING_WORD = { (!ENDING_RULE ~ letter)* ~
                (INTER_D | ENDING_PS | ENDING_D | ENDING_S | ENDING_CONS)
              }

word_ending = _ { ((ENDING_WORD | word) ~ (space | EOI))+ }


// Digraphs producers. (vowel)(const)(const) that triggers the general digraph rule
Nn = { ("ñ" | "Ñ" ) }
Y = { ("y" | "Y" ) }
W = { ("w" | "W" ) }
DIGRAPH_1 = { (B|C|Cc|D|F|G|H|M|N|P|Q|T|X|Y) }
DIGRAPH_B = { (B ~ DIGRAPH_1) }
DIGRAPH_C = { (C ~ DIGRAPH_1) }
DIGRAPH_F = { (F ~ DIGRAPH_1) }
DIGRAPH_G = { (G ~ DIGRAPH_1) }
DIGRAPH_P = { (P ~ DIGRAPH_1) }

DIGRAPH_2 = { (DIGRAPH_1 | L) }
DIGRAPH_D = { (D ~ DIGRAPH_2) }
DIGRAPH_T = { (T ~ DIGRAPH_2) }

DIGRAPH_3 = { (DIGRAPH_2 | R) }
DIGRAPH_J = { (J ~ DIGRAPH_3) }
DIGRAPH_L = { (L ~ DIGRAPH_3) }
DIGRAPH_X = { (X ~ DIGRAPH_3) }
DIGRAPH_Z = { (Z ~ DIGRAPH_3) }

DIGRAPH_S = { (S ~ (DIGRAPH_3 | K | Nn)) }

DIGRAPH_M = { (M ~ M) | (M ~ N) }
DIGRAPH_N = { (N ~ M) | (N ~ N) }
DIGRAPH_R = { (R ~ N) }

DIGRAPH = { (DIGRAPH_B | DIGRAPH_C | DIGRAPH_D |
             DIGRAPH_F | DIGRAPH_G | DIGRAPH_J |
             DIGRAPH_L | DIGRAPH_M | DIGRAPH_N |
             DIGRAPH_R | DIGRAPH_S | DIGRAPH_T |
             DIGRAPH_X | DIGRAPH_Z) }

GEN_DIGRAPH = { (vowel ~ DIGRAPH) }

// atlántico => âl-lántico | orla => ôl-la | adlátere => âl-látere | tesla => têl-la ...
DIGRAPH_EXP_L = { (vowel ~ (D|J|R|S|T|X|Z) ~ L) }

// abstracto => âttrâtto | adscrito => âccrito | perspectiva => pêrppêttiba
DIGRAPH_EXP_BDNR = { (vowel ~ (B|D|N|R) ~ S ~ (B|C|Cc|D|F|G|H|J|K|L|M|N|P|Q|S|T|V|W|X|Y|Z)) }

// aerotransporte => aerotrâpporte | translado => trâl-lado | transcendente => trâççendente | postoperatorio => pôttoperatorio | postpalatal => pôppalatal
TR = { T ~ R }
NS = { N ~ S }
ST = { S ~ T }
A = { "a" | "A" }
O = { "o" | "O" }
TRANS1 = { (TR|P) }
TRANS2 = { (A|O) }
TRANS3 = { (NS|ST) }
DIGRAPH_EXP_TRANS = { TRANS1 ~ TRANS2 ~ TRANS3 ~ (B|C|Cc|D|F|G|H|J|K|L|M|N|P|Q|S|T|V|W|X|Y|Z) }

// intersticial / solsticio / superstición / cárstico => interttiçiâh / çorttiçio / çuperttiçión / cárttico
DIGRAPH_EXP_LSTRST = { (vowel ~ (L|R) ~ S ~ T) }

digraph = _{ ((
              (DIGRAPH_EXP_LSTRST | DIGRAPH_EXP_TRANS | DIGRAPH_EXP_BDNR |
               DIGRAPH_EXP_L | GEN_DIGRAPH)
              | letter)+ ~ space?)+ }

exception = _ { (word ~ space?)+ }

// Contractions and other word interaction rules
ENDING_L = { (!L ~ letter)+ ~ L ~ space+ ~ (B|C|Cc|D|F|G|H|J|K|L|M|N|Nn|P|Q|S|T|V|W|X|Y|Z) }
word_interaction = _ { ((ENDING_L | word) ~ space*)+ }
